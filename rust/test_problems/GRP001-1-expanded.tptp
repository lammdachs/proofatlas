% GRP001-1 with axioms expanded
% Domain   : Group Theory
% Problem  : X^2 = identity => commutativity

% Group theory axioms from GRP003-0.ax
cnf(left_identity,axiom,
    product(identity,X,X) ).

cnf(right_identity,axiom,
    product(X,identity,X) ).

cnf(left_inverse,axiom,
    product(inverse(X),X,identity) ).

cnf(right_inverse,axiom,
    product(X,inverse(X),identity) ).

cnf(total_function1,axiom,
    product(X,Y,multiply(X,Y)) ).

cnf(total_function2,axiom,
    ~product(X,Y,Z) | ~product(X,Y,W) | Z = W ).

cnf(associativity1,axiom,
    ~product(X,Y,U) | ~product(Y,Z,V) | ~product(U,Z,W) | product(X,V,W) ).

cnf(associativity2,axiom,
    ~product(X,Y,U) | ~product(Y,Z,V) | ~product(X,V,W) | product(U,Z,W) ).

% Problem specific axioms
cnf(square_element,hypothesis,
    product(X,X,identity) ).

cnf(a_times_b_is_c,negated_conjecture,
    product(a,b,c) ).

cnf(prove_b_times_a_is_c,negated_conjecture,
    ~product(b,a,c) ).